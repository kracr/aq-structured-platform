# The query returns percentage count of rating groups across different spatial locations
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX aq: <http://www.semanticweb.org/saadf/ontologies/2021/2/AirQualityOntology#>
PREFIX saqi: <https://kracr.iiitd.edu.in/ontology/saqi#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ns0: <https://kracr.iiitd.edu.in/ontology/>
SELECT ?percption_govt ?placeName (xsd:integer(COUNT( ?percption_govt) *100 / ?count2) as ?perceptionPercentage)
    WHERE {
      ?person rdf:type saqi:Person ;
      saqi:hasIndividualPerception ?perception ;
      saqi:isPartOfSocialCohort ?cohort ;
      saqi:livesIn ?place .
  	  ?perception saqi:delhiGovtWorkPerception ?percption_govt .
      ?place saqi:hasName ?placeName .
      {
        SELECT (COUNT( ?placeName) as ?count2) ?placeName
        WHERE {
                ?person rdf:type saqi:Person ;
                saqi:hasIndividualPerception ?perception ;
                saqi:isPartOfSocialCohort ?cohort ;
                saqi:livesIn ?place .
      			?perception saqi:delhiGovtWorkPerception ?percption_govt .
                ?place saqi:hasName ?placeName .
        }
        GROUP BY ?placeName
      }	

    } 
GROUP BY ?percption_govt ?placeName ?count2
ORDER BY ?placeName ?percption_govt ?perceptionPercentage